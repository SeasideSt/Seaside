"
This configuration class holds attributes needed by classes in the Request Handling layer.
"
Class {
	#name : #WARequestHandlingConfiguration,
	#superclass : #WASystemConfiguration,
	#category : 'Seaside-Core-RequestHandling'
}

{ #category : #description }
WARequestHandlingConfiguration >> describeOn: config [ 
	(config string: #charSet)
		beAdvanced;
		label: 'Character Set';
		comment: 'The character set reported to the browser. This is only used if you set the encoding in the server to ''(none)''.';
		default: 'utf-8'.
	(config classBinding: #documentClass)
		beAdvanced;
		options: [ self documentClasses ];
		default: WAHtmlDocument.
	(config classBinding: #scriptGeneratorClass)
		beAdvanced;
		options: [ self scriptGeneratorClasses ];
		default: WADefaultScriptGenerator.
	(config classBinding: #responseGenerator)
		beAdvanced;
		options: [ self responseGenerators ];
		default: WAResponseGenerator.
	(config list: #serverProtocol)
		beAdvanced;
		group: #server;
		comment: 'The protocol in URLs generated by Seaside.';
		options: [ self serverProtocols ].
	(config string: #serverHostname)
		beAdvanced;
		group: #server;
		comment: 'The hostname in URLs generated by Seaside.'.
	(config integer: #serverPort)
		beAdvanced;
		group: #server;
		comment: 'The port in URLs generated by Seaside.'.
	(config string: #serverPath)
		group: #server;
		comment: 'The path in URLs generated by Seaside.'
]

{ #category : #private }
WARequestHandlingConfiguration >> documentClasses [
	^ WADocument withAllSubclasses asSortedCollection: [ :a :b | a name <= b name ]
]

{ #category : #private }
WARequestHandlingConfiguration >> responseGenerators [
	^ WAResponseGenerator withAllSubclasses asSortedCollection: [ :a :b | a name <= b name ]
]

{ #category : #private }
WARequestHandlingConfiguration >> scriptGeneratorClasses [
	^ WAScriptGenerator allSubclasses asSortedCollection: [ :a :b | a name <= b name ]
]

{ #category : #private }
WARequestHandlingConfiguration >> serverProtocols [
	^ #(nil 'http' 'https')
]
